plugins {
    id 'multiloader-common'
}

def mcVersion = project.property('minecraft_version')
def cmp = compareVersions(mcVersion, '1.20.2')

// Conditional plugin application
if (cmp < 0) {
    // < 1.20.2 -> legacyforge + sponge
    apply plugin: 'net.neoforged.moddev.legacyforge'
    apply plugin: 'org.spongepowered.gradle.vanilla'

    logger.lifecycle("Applying legacy Neoforge + Sponge for Minecraft $mcVersion")

} else {
    // >= 1.20.2 -> new neoforge moddev only
    apply plugin: 'net.neoforged.moddev'

    logger.lifecycle("Applying Neoforge moddev for Minecraft $mcVersion")
}

// Conditional forge or neoforge blocks
if (cmp < 0) {
    minecraft {
        version(minecraft_version)
        def aw = file("src/main/resources/${mod_id}.accesswidener")
        if (aw.exists()) {
            accessWideners(aw)
        }
    }
}

if (cmp >= 0) {
    neoForge {
        neoFormVersion = neo_form_version
        // Automatically enable AccessTransformers if the file exists
        def at = file('src/main/resources/META-INF/accesstransformer.cfg')
        if (at.exists()) {
            accessTransformers.from(at.absolutePath)
        }
        parchment {
            minecraftVersion = parchment_minecraft
            mappingsVersion = parchment_version
        }
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'

    // The library to generate code
    implementation 'com.squareup:javapoet:1.13.0'

    // Google's auto-service to find the processor
    compileOnly 'com.google.auto.service:auto-service:1.0.1'
    annotationProcessor 'com.google.auto.service:auto-service:1.0.1'
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each { variant ->
    configurations.named("$variant") {
        attributes {
            attribute(loaderAttribute, 'common')
        }
    }
}
sourceSets.configureEach {
    [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName].each { variant->
        configurations.named("$variant") {
            attributes {
                attribute(loaderAttribute, 'common')
            }
        }
    }
}
